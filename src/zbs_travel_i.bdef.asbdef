managed implementation in class zbp_bs_travel_i unique;
strict ( 2 );
with draft;
define behavior for zbs_travel_i alias Travel
draft table zbs_travel_d
persistent table zbs_travel
with additional save
lock master
total etag LastChangedAt
authorization master ( global, instance )
etag master LocalLastChangedAt
{
  create ( authorization : global );
  update;
  delete;
  field ( readonly ) TravelUuid;
  field ( numbering : managed ) TravelUuid;
  field ( readonly ) TravelId, OverallStatus;
  // feature control
  field(features : instance)BookingFee;
  association _Booking { create; with draft; }
  draft action Edit;
  draft action Discard;
  draft action Resume;
  draft action Activate optimized;
  draft determine action Prepare
 // validation on draft phase
  {
validation validatecustomer ;
validation validatragency ;
validation validatedate; }


  // deteramation .
  determination savetravelid on save { create; update; }
  determination setoverallstatus on save { create; update; }

// validatation .
validation validatecustomer on save {create;field customerid ;}
validation validatragency on save {create;field AgencyId ;}
validation validatedate on save {create;field BeginDate,EndDate ;}
  // action
  action(features : instance) accepttravel result [1] $self;
  action(features : instance) rejecttravel result [1] $self;
  action(features : instance) discount parameter zbs_discount result [1] $self { default function GetDefaultsFordeductdiscounts; }
  determination calculatetotalprice on modify { create; field BookingFee, CurrencyCode; }

  determine action validatecustomerid {validation validatecustomer;}
  determine action validateagencyid {validation validatragency;}
  determine action validatedatesval {validation validatedate;}
  side effects
  {
    field BookingFee affects field TotalPrice;
   determine action validatecustomerid executed on field CustomerId affects messages;
   determine action validateagencyid executed on field AgencyId affects messages;
   determine action validatedatesval executed on field BeginDate , field EndDate  affects messages;

    }

  internal action recalculatetotalprice;
  mapping for zbs_travel
    {
      TravelUuid         = travel_uuid;
      TravelId           = travel_id;
      AgencyId           = agency_id;
      CustomerId         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      OverallStatus      = overall_status;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}

define behavior for zbs_booking_i alias Booking
draft table zbs_booking_d
persistent table zbs_booking
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
{
  update;
  delete;
  field ( readonly ) BookingUuid, TravelUuid, BookingId, BookingDate;
  field ( numbering : managed ) BookingUuid;
  association _Travel { with draft; }
  association _Bookingsupplements { create; with draft; }

  determination setbookingid on save { create;update; }
  determination setbookingdate on save { create;update;}
  determination calculatetotalprice on modify { create;update; field FlightPrice, CurrencyCode; }

  side effects
  {
    field FlightPrice affects field _Travel.TotalPrice; }

  mapping for zbs_booking
    {
      BookingUuid   = booking_uuid;
      TravelUuid    = parent_uuid;
      BookingId     = booking_id;
      BookingDate   = booking_date;
      CustomerId    = customer_id;
      CarrierId     = carrier_id;
      ConnectionId  = connection_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      CurrencyCode  = currency_code;
      BookingStatus = booking_status;
      LastChangedAt = last_changed_at;
    }
}

define behavior for ZBS_BOOKING_SUPL_i alias Bookingsupplement
draft table zbs_booking_supd
persistent table zbs_booking_supl
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
{
  update;
  delete;
  field ( numbering : managed ) BooksupplUuid;
  field ( readonly ) RootUuid, BooksupplUuid, ParentUuid, BookingSupplementId;
  association _Travel { with draft; }
  association _Booking { with draft; }

  determination bookingsupplement on save { create;}
  determination calculatetotalprice on modify { create;update; field Price, CurrencyCode; }

  side effects
  {
    field Price affects field _Travel.TotalPrice; }

  mapping for zbs_booking_supl
    {
      BooksupplUuid       = booksuppl_uuid;
      RootUuid            = root_uuid;
      ParentUuid          = parent_uuid;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
    }
}